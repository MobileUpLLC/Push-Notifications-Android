plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
}

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    compileSdk target_sdk_version

    defaultConfig {
        minSdk min_sdk_version
        targetSdk target_sdk_version

        buildConfigField "String", "ACCESS_KEY", properties.getProperty("api.unsplash.access_token", "")
    }

    flavorDimensions "platform"
    productFlavors {

        universal {
            dimension "platform"
        }

        gms {
            dimension "platform"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-opt-in=kotlinx.serialization.ExperimentalSerializationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.FlowPreview"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    // Kotlin
    implementation libs.kotlin.datetime
    implementation libs.coroutines.core
    implementation libs.coroutines.android

    // UI
    implementation libs.compose.ui
    implementation libs.compose.material
    implementation libs.activity.compose
    debugImplementation libs.compose.tooling

    // DI
    implementation libs.koin.android

    // Serialization
    implementation libs.serialization.json

    // Network
    implementation libs.retrofit.core
    implementation libs.retrofit.converter.serialization
    implementation libs.okhttp.logging

    // Architecture
    implementation libs.bundles.sesame
    implementation libs.bundles.decompose
    implementation libs.bundles.replica
    debugImplementation libs.replica.devtools.debug
    releaseImplementation libs.replica.devtools.release

    // Debugging
    implementation libs.timber

    // Firebase Services
    implementation libs.firebase.messaging
    implementation libs.gms.base

    // Huawei Mobile Services
    universalImplementation libs.hms.core
    universalImplementation libs.hms.push
}