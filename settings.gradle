pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        maven { url 'https://developer.huawei.com/repo/' }
    }
}

enableFeaturePreview('VERSION_CATALOGS')

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven { url 'https://developer.huawei.com/repo/' }
    }

    versionCatalogs {
        libs {
            // Kotlin
            alias('kotlin-datetime').to('org.jetbrains.kotlinx', 'kotlinx-datetime').version('0.3.2')

            version('coroutines', '1.6.0-RC')
            alias('coroutines-core').to('org.jetbrains.kotlinx', 'kotlinx-coroutines-core').versionRef('coroutines')
            alias('coroutines-android').to('org.jetbrains.kotlinx', 'kotlinx-coroutines-android').versionRef('coroutines')

            // Android
            alias('android-desugar').to('com.android.tools', 'desugar_jdk_libs').version('1.1.5')
            alias('androidx-lifecycle-process').to('androidx.lifecycle', 'lifecycle-process').version('2.4.0')

            // UI
            version('compose', '1.2.0-alpha08')
            alias('compose-ui').to('androidx.compose.ui', 'ui').versionRef('compose')
            alias('compose-material').to('androidx.compose.material', 'material').versionRef('compose')
            alias('compose-tooling').to('androidx.compose.ui', 'ui-tooling').versionRef('compose')
            alias('activity-compose').to('androidx.activity', 'activity-compose').version('1.4.0')
            alias('appcompat').to('androidx.appcompat', 'appcompat').version('1.4.0')
            alias('coil').to('io.coil-kt', 'coil-compose').version('1.4.0')

            // Architecture
            version('sesame', '1.5.0')
            alias('sesame-localizedString').to('com.github.aartikov', 'sesame-localized-string').versionRef('sesame')
            alias('sesame-composeForm').to('com.github.aartikov', 'sesame-compose-form').versionRef('sesame')
            bundle('sesame', ['sesame-localizedString', 'sesame-composeForm'])

            version('decompose', '0.5.0')
            alias('decompose-core').to('com.arkivanov.decompose', 'decompose').versionRef('decompose')
            alias('decompose-compose').to('com.arkivanov.decompose', 'extensions-compose-jetpack').versionRef('decompose')
            bundle('decompose', ['decompose-core', 'decompose-compose'])

            version('replica', '1.0.0-alpha4')
            alias('replica-core').to('com.github.aartikov', 'replica-core').versionRef('replica')
            alias('replica-algebra').to('com.github.aartikov', 'replica-algebra').versionRef('replica')
            alias('replica-android-network').to('com.github.aartikov', 'replica-android-network').versionRef('replica')
            alias('replica-decompose').to('com.github.aartikov', 'replica-decompose').versionRef('replica')
            alias('replica-devtools-debug').to('com.github.aartikov', 'replica-devtools').versionRef('replica')
            alias('replica-devtools-release').to('com.github.aartikov', 'replica-devtools-noop').versionRef('replica')
            bundle('replica', ['replica-core', 'replica-algebra', 'replica-android-network', 'replica-decompose'])

            // Serialization
            version('serialization', '1.3.0')
            alias('serialization-core').to('org.jetbrains.kotlinx', 'kotlinx-serialization-core').versionRef('serialization')
            alias('serialization-json').to('org.jetbrains.kotlinx', 'kotlinx-serialization-json').versionRef('serialization')

            // Network
            alias('retrofit-core').to('com.squareup.retrofit2', 'retrofit').version('2.9.0')
            alias('retrofit-converter-serialization').to('com.jakewharton.retrofit', 'retrofit2-kotlinx-serialization-converter').version('0.8.0')
            alias('okhttp-logging').to('com.squareup.okhttp3', 'logging-interceptor').version('4.9.2')

            // DI
            version('koin', '3.1.2')
            alias('koin-android').to('io.insert-koin', 'koin-android').versionRef('koin')
            alias('koinTest').to('io.insert-koin', 'koin-test').versionRef('koin')
            alias('koinTestJunit').to('io.insert-koin', 'koin-test-junit4').versionRef('koin')
            bundle('koin-test', ['koinTest', 'koinTestJunit'])

            // Code quality
            alias('detekt-formatting').to('io.gitlab.arturbosch.detekt', 'detekt-formatting').version("1.18.1")

            // Debugging
            alias('timber').to('com.jakewharton.timber', 'timber').version('5.0.1')

            // Testing
            alias('junit').to('junit', 'junit').version('4.13.2')
            alias('extJunit').to('androidx.test.ext', 'junit-ktx').version('1.1.3')
            alias('mockWebServer').to('com.squareup.okhttp3', 'mockwebserver').version('4.3.1')
            alias('awaitility').to('org.awaitility', 'awaitility-kotlin').version('4.2.0')
            alias('robolectric').to('org.robolectric', 'robolectric').version('4.7.3')

            // Huawei Mobile Service
            alias('hms-core').to('com.huawei.agconnect', 'agconnect-core').version('1.6.4.300')
            alias('hms-push').to('com.huawei.hms', 'push').version('6.3.0.304')

            // Firebase Service
            alias('gms-base').to('com.google.android.gms', 'play-services-base').version('18.0.1')
            alias('firebase-messaging').to('com.google.firebase', 'firebase-messaging-ktx').version('23.0.0')
        }
    }
}
rootProject.name = "Connect notification on Android"
include ':app'
include ':core'
include ':features'